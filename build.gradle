import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.7.3'
        classpath 'org.javamodularity:moduleplugin:1.8.15'
    }
}

plugins {
    id 'idea'
    id 'application'
    id 'com.google.osdetector'           version '1.7.3'
    id 'org.javamodularity.moduleplugin' version '1.8.15'
    id 'net.nemerosa.versioning'         version '3.1.0'
}

normalization {
    runtimeClasspath {
        ignore('/META-INF/MANIFEST.MF')
    }
}

repositories {
    mavenCentral()
}

Date buildTimeAndDate = new Date()
ext {
    buildDate  = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime  = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    platform   = osdetector.os == 'osx' ? osdetector.arch == 'aarch_64' ? 'mac-aarch64' : 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os == 'linux' ? osdetector.arch == 'aarch_64' ? 'linux-aarch64' : 'linux' : osdetector.os
    buildTime  = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

dependencies {
    implementation "org.openjfx:javafx-base:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${platform}"
}

description = "TimeBuddyFX is a JavaFX implementation of the TimeBuddy iOS app"

application {
    mainModule.set('eu.hansolo.fx.timebuddy')
    mainClass.set('eu.hansolo.fx.timebuddy.Launcher')
}

jar {

    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect {  it.isDirectory() ? it : zipTree(it)  }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    manifest {
        attributes(
                'Built-By'              : System.properties['user.name'],
                'Created-By'            : System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
                'Build-Date'            : project.buildDate,
                'Build-Time'            : project.buildTime,
                'Build-Revision'        : versioning.info.commit,
                'Specification-Title'   : project.name,
                'Specification-Version' : project.version,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.version,
                'Bundle-Name'           : project.name,
                'Bundle-License'        : 'https://www.apache.org/licenses/LICENSE-2.0;description=Apache License Version 2.0;link=https://spdx.org/licenses/Apache-2.0.html',
                'Bundle-Description'    : description,
                'Bundle-Version'        : project.version,
                'Bundle-SymbolicName'   : 'eu.hansolo.fx.timebuddy',
                'Class-Path'            : '${project.name}-${project.version}.jar',
                'Main-Class'            : 'eu.hansolo.fx.timebuddy.Launcher'
        )
    }
}

// start the TimeBuddy from gradle
tasks.register('TimeBuddy', JavaExec) {
    mainClass = "eu.hansolo.fx.timebuddy.Launcher"
    classpath = sourceSets.main.runtimeClasspath
}

run {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--module', mainClassName
        ]
        classpath = files()
    }
}